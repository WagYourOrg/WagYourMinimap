plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.10.0-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.18.2:2022.07.03@zip")
        }
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.16.5+build.9:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven {
            name = "ldtteam"
            url = "https://maven.parchmentmc.net/"
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

task fabricDist(type: Copy) {
    from new File(project(":fabric").buildDir, "libs")
    into new File(project.rootDir, "dist")
    rename {
        it.replace ".jar", "-fabric.jar"
    }
}


task forgeDist(type: Copy) {
    from new File(project(":forge").buildDir, "libs")
    into new File(project.rootDir, "dist")
    rename {
        it.replace ".jar", "-forge.jar"
    }
}

task clearDist(type: Delete) {
    delete new File(project.rootDir, "dist")
}

task createDist(dependsOn: build) {}

createDist.finalizedBy fabricDist
createDist.finalizedBy forgeDist

clean.finalizedBy(clearDist)